plugins {
	id 'java-library'
	id 'java-gradle-plugin'
	id 'jvm-test-suite'
	id 'maven-publish'
	id 'nokeebuild.use-junit-platform-in-test-suites'
	id 'nokeebuild.use-latest-java-lts-in-test-suites'
	id 'nokeebuild.publishing'
	id 'nokeebuild.signing'
}

version = '1.0'
description = 'Gradle utilities for fast development'

dependencies {
	compileOnlyApi gradleApi()
	compileOnly(project(':commons-backports')) { because 'ProviderConvertible' }
}

tasks.named('javadoc', Javadoc) {
	options {
		windowTitle 'Nokee Commons: Gradle'
	}
}

testing {
	suites {
		test {
			dependencies {
				implementation gradleTestKit()
				implementation 'dev.gradleplugins:gradle-build-script'
				implementation project(':commons-hamcrest')
				implementation libs.mockito.core
				implementation libs.apache.commons.io
				implementation libs.mockito.junit
				implementation libs.hamcrest.optional
			}
		}
	}
}
