plugins {
	id 'java-library'
	id 'java-gradle-plugin'
	id 'java-test-fixtures'
	id 'jvm-test-suite'
	id 'maven-publish'
	id 'nokeebuild.use-junit-platform-in-test-suites'
	id 'nokeebuild.use-latest-java-lts-in-test-suites'
	id 'nokeebuild.publishing'
	id 'nokeebuild.signing'
}

version = '1.3-SNAPSHOT'
description = 'Gradle utilities for fast development'

dependencies {
	compileOnly gradleApi()
	compileOnly(project(':commons-backports')) { because 'ProviderConvertible' }

	testFixturesCompileOnlyApi gradleApi()
	testFixturesImplementation 'org.opentest4j:opentest4j:1.3.0'
	testFixturesImplementation project(':commons-hamcrest')
	testFixturesApi 'dev.nokee.commons:test-gradle-projects:latest.integration'
	testFixturesApi 'dev.gradleplugins:gradle-runner-kit:latest.release'
	testFixturesImplementation testFixtures('dev.gradleplugins:gradle-runner-kit:latest.release')
	testFixturesApi 'org.junit.jupiter:junit-jupiter-api:5.12.0'
	testFixturesImplementation project(':commons-names')
}

components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) { skip() }
components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) {	skip() }

tasks.named('javadoc', Javadoc) {
	options {
		windowTitle 'Nokee Commons: Gradle'
	}
}

testing {
	suites {
		test {
			dependencies {
				implementation gradleTestKit()
				implementation 'dev.gradleplugins:gradle-build-script'
				implementation project(':commons-hamcrest')
				implementation libs.mockito.core
				implementation libs.apache.commons.io
				implementation libs.mockito.junit
				implementation libs.hamcrest.optional
			}
		}
	}
}
