pluginManagement {
	includeBuild 'gradle/plugins/nokeebuild-plugins'
}

rootProject.name = 'nokee-commons'

include 'commons-backports'
include 'commons-names'
include 'commons-gradle'

includeBuild 'commons-hamcrest'
includeBuild 'commons-build-script'

dependencyResolutionManagement {
	repositoriesMode = RepositoriesMode.PREFER_SETTINGS
	repositories {
		mavenCentral()
	}
}

gradle.allprojects {
	group = 'dev.nokee.commons'

	pluginManager.withPlugin('java-base') {
		java {
			sourceCompatibility = JavaVersion.VERSION_1_8
			targetCompatibility = JavaVersion.VERSION_1_8
		}

		ext {
			gradleApi = { version -> "dev.gradleplugins:gradle-api:$version" }
		}
	}
	pluginManager.withPlugin('java-library') {
		java {
			withSourcesJar()
			withJavadocJar()
		}

		tasks.named('javadoc', Javadoc) {
			options {
				showFromPublic()
				links += 'https://docs.gradle.org/current/javadoc/'
				classpath += configurations.detachedConfiguration(dependencies.create('dev.gradleplugins:gradle-api:8.11'))
				// TODO: Maybe infer windowsTitle based on project name
			}
		}
	}
	pluginManager.withPlugin('maven-publish') {
		publishing {
			repositories {
				maven {
					name = 'ossrhRelease'
					url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
					credentials(PasswordCredentials)
				}
			}
			publications {
				pluginManager.withPlugin('java-library') {
					java(MavenPublication) {
						from(components.java)
					}
				}
			}
		}
	}
}
